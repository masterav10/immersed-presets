plugins {
    id 'java-gradle-plugin'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'org.bytedeco.javacpp'
            implementationClass = 'org.bytedeco.gradle.JavaCppPlugin'
        }
    }
}

// Workaround for no sources
// https://stackoverflow.com/questions/22694199/gradle-api-sources-and-doc-when-writing-gradle-plugins
plugins.withType(EclipsePlugin) {
    plugins.withType(JavaBasePlugin) {
        eclipse {
            classpath {
                file {
                    whenMerged { classpath ->
                        String gradleHome = gradle.getGradleHomeDir()
                            .absolutePath
                            .replace(File.separator, '/')
                        String gradleSourceDirectory = "${gradleHome}/src"
                        classpath.entries.each { entry ->
                            if (entry in org.gradle.plugins.ide.eclipse.model.AbstractLibrary
                                    && entry.library.path.contains('generated-gradle-jars')) {
                                entry.sourcePath =
                                    new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory()
                                        .fromPath(gradleSourceDirectory)
                            }
                        }
                    }
                }
            }
        }
    }
}

// https://github.com/gradle/gradle/issues/9996
tasks['cleanEclipse'].dependsOn clean
tasks['eclipseClasspath'].dependsOn pluginUnderTestMetadata